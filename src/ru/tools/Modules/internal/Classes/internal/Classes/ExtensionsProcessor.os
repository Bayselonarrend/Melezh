#Использовать "./internal"

Перем ОбъектОПИ;
Перем ХранилищеНастроек;
Перем КаталогРасширений;
Перем КэшРасширений;
Перем ПроцессорДействий;

#Область СлужебныйПрограммныйИнтерфейс

Процедура Инициализировать(ОбъектОПИ_, ХранилищеНастроек_, КаталогРасширений_, ПроцессорДействий_) Экспорт
	
	ОбъектОПИ          = ОбъектОПИ_;
	ХранилищеНастроек  = ХранилищеНастроек_;
    КаталогРасширений  = КаталогРасширений_;
    КэшРасширений      = Новый Соответствие;
    ПроцессорДействий  = ПроцессорДействий_;

    ДополнитьСоставРасширениями();
	
КонецПроцедуры

Процедура ДополнитьСоставРасширениями() Экспорт

    КаталогиРасширений = ПолучитьКаталогиРасширений();

    Для Каждого ТекущийКаталогРасширений Из КаталогиРасширений Цикл
        
        ФайлыРасширений = НайтиФайлы(ТекущийКаталогРасширений, "*.os");

        Для Каждого ФайлРасширения Из ФайлыРасширений Цикл

            ПодключитьФайлРасширения(ФайлРасширения);

        КонецЦикла;

    КонецЦикла;

КонецПроцедуры

Функция ПодключитьФайлРасширения(ФайлРасширения, Тест = Ложь) Экспорт

    Попытка

        ИмяРасширения = ФайлРасширения.ИмяБезРасширения;

        Если Не Toolbox.СтрокаНачинаетсяСБуквы(ИмяРасширения) Тогда

            ОписаниеПроблемы = СтрШаблон("Ошибка применения расширения %1: имя модуля должно начинаться с буквы", ИмяРасширения);
            ОбработатьИсключениеПодключения(ФайлРасширения, ОписаниеПроблемы, Тест);
            Возврат ОписаниеПроблемы;

        КонецЕсли;

        Попытка 
            ТестОбъекта = ЗагрузитьСценарий(ФайлРасширения.ПолноеИмя, Новый Структура("Melezh", ПроцессорДействий));
        Исключение
            ОписаниеПроблемы = СтрШаблон("Ошибка применения расширения: %1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
            ОбработатьИсключениеПодключения(ФайлРасширения, ОписаниеПроблемы, Тест);
            Возврат ОписаниеПроблемы;
        КонецПопытки;

        ТаблицаПараметров = РазобратьМодуль(ФайлРасширения);

        Если Не Тест Тогда
            ОбъектОПИ.ДополнитьКэшСостава(ФайлРасширения.ИмяБезРасширения, ТаблицаПараметров); 
            ПроцессорДействий.ПодключитьСценарийРасширения(ФайлРасширения.ПолноеИмя, ФайлРасширения.ИмяБезРасширения);
        КонецЕсли;

    Исключение

        ОписаниеПроблемы = СтрШаблон("Ошибка применения расширения: %1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
        ОбработатьИсключениеПодключения(ФайлРасширения, ОписаниеПроблемы, Тест);
        Возврат ОписаниеПроблемы;

    КонецПопытки;

    Возврат Неопределено;

КонецФункции

Функция ПолучитьСписокРасширений() Экспорт

    МассивРасширений = Новый Массив;

    Для Каждого Расширение Из КэшРасширений Цикл

        ТекущееРасширение = Новый Структура;
        ТекущееРасширение.Вставить("name"    , Расширение.Ключ);
        ТекущееРасширение.Вставить("filepath", Расширение.Значение["filepath"]);
        ТекущееРасширение.Вставить("count"   , Расширение.Значение["count"]);
        
        МассивРасширений.Добавить(ТекущееРасширение);

    КонецЦикла;

    Возврат Новый Структура("result,data", Истина, МассивРасширений);

КонецФункции

Функция ОбновитьСписокРасширений() Экспорт

    КэшРасширений = Новый Соответствие;
    ОбъектОПИ.ИнициализироватьОсновныеСписки();
    ПроцессорДействий.ОчиститьСписокАктивныхРасширений();
    ДополнитьСоставРасширениями();
    
    Возврат Новый Структура("result", Истина);

КонецФункции

Функция ПолучитьТекстРасширения(ИмяМодуля) Экспорт

    Расширение = КэшРасширений.Получить(ИмяМодуля);

    Если Расширение = Неопределено Тогда
        Результат = Новый Структура("result,error", Ложь, "Расширение с указанным именем не найдено!");
    Иначе

        Текст = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(Расширение["filepath"]));
        Результат = Новый Структура("result,text", Истина, Текст);

    КонецЕсли;

    Возврат Результат;

КонецФункции

Функция СохранитьТекстРасширения(ИмяМодуля, ТекстМодуля) Экспорт

    Расширение = КэшРасширений.Получить(ИмяМодуля);

    Если Расширение = Неопределено Тогда
        Результат = Новый Структура("result,error", Ложь, "Расширение с указанным именем не найдено!");
    Иначе

        ТекстДД   = ПолучитьДвоичныеДанныеИзСтроки(ТекстМодуля);
        ТекстДД.Записать(Расширение["filepath"]);

        Результат = ОбновитьСписокРасширений();

    КонецЕсли;

    Возврат Результат;

КонецФункции

Функция ПолучитьСписокКаталоговРасширений() Экспорт
    Возврат Новый Структура("result,data", Истина, ПолучитьКаталогиРасширений());
КонецФункции

Функция СоздатьФайлРасширения(ИмяМодуля, КаталогСоздания) Экспорт

    Если Не Toolbox.СтрокаНачинаетсяСБуквы(ИмяМодуля) Тогда
        Возврат Новый Структура("result,error,code", Ложь, "Имя модуля должно начинаться с буквы!", 400);
    КонецЕсли;

    КаталогиРасширений = ПолучитьКаталогиРасширений();
    ПереданДоступный   = Ложь;
    
    Для Каждого ТекущийКаталогРасширений Из КаталогиРасширений Цикл

        Если ТекущийКаталогРасширений = КаталогСоздания Тогда
            ПереданДоступный = Истина;
        КонецЕсли;

    КонецЦикла;

    Если Не ПереданДоступный Тогда
        Возврат Новый Структура("result,error,code", Ложь, "Передан некорректный каталог расширений", 404);
    КонецЕсли;

    Если КэшРасширений.Получить(ИмяМодуля) <> Неопределено Тогда
        Возврат Новый Структура("result,error,code", Ложь, "Расширение с таким именем уже существует", 400);
    КонецЕсли;

    ПутьСохранения = СтрЗаменить(КаталогСоздания, "\", "/");
    ПутьСохранения = ?(СтрЗаканчиваетсяНа(ПутьСохранения, "/"), ПутьСохранения, ПутьСохранения + "/");
    ПутьСохранения = СтрШаблон("%1%2.os", ПутьСохранения, ИмяМодуля);

    ПолучитьДвоичныеДанныеИзСтроки("").Записать(ПутьСохранения);

    Результат = ОбновитьСписокРасширений();

    Возврат Результат;
    
КонецФункции

Функция УдалитьФайлРасширения(ИмяМодуля) Экспорт

    Расширение = КэшРасширений.Получить(ИмяМодуля);

    Если Расширение = Неопределено Тогда
        Результат = Новый Структура("result,error", Ложь, "Расширение с указанным именем не найдено!");
    Иначе
      
        УдалитьФайлы(Расширение["filepath"]);
        Результат = ОбновитьСписокРасширений();

    КонецЕсли;

    Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазобратьМодуль(Модуль)
    
    ТекущийМодуль     = Модуль.ИмяБезРасширения;
    ТекущаяОбласть    = "Основные методы";
    
    Парсер         = Новый EmbeddedLanguageParser;
    ДокументМодуля = Новый ТекстовыйДокумент;
    ДокументМодуля.Прочитать(Модуль.ПолноеИмя);
    
    ТекстМодуля     = ДокументМодуля.ПолучитьТекст();  
    СтруктураМодуля = Парсер.Разобрать(ТекстМодуля);
    
    ТекущийИндекс = Новый Структура;
    ТекущийИндекс.Вставить("module"     , ТекущийМодуль);
    ТекущийИндекс.Вставить("library"    , ТекущийМодуль);
    ТекущийИндекс.Вставить("self_depend", СтрЧислоВхождений(ТекстМодуля, ТекущийМодуль) > 1);
        
    МассивМетодов        = Новый Массив;
    МассивМетодовРегиона = Новый Массив;
    МассивРегионов       = Новый Массив;
    
    Для Каждого Метод Из СтруктураМодуля.Объявления Цикл
        
        Если Метод.Тип = "ИнструкцияПрепроцессораОбласть" Тогда

            Если ЗначениеЗаполнено(МассивМетодовРегиона) Тогда
                СтруктураРегиона = Новый Структура("name,methods", ТекущаяОбласть, МассивМетодовРегиона);
                МассивРегионов.Добавить(СтруктураРегиона);
                МассивМетодовРегиона = Новый Массив;
            КонецЕсли;

            ТекущаяОбласть = Синонимайзер(Метод.Имя);
        КонецЕсли;
        
        Если Метод.Тип = "ОбъявлениеМетода" И Метод.Сигнатура.Экспорт = Истина Тогда
            
            ИмяМетода    = Метод.Сигнатура.Имя;
            ТекущийМетод = РазобратьКомментарийМетода(ДокументМодуля, Метод, Модуль);	 

            Если ТекущийМетод = Неопределено Тогда
                Продолжить;
            КонецЕсли;

            ТекущийМетод.Вставить("region", ТекущаяОбласть);
            
            МассивМетодов.Добавить(ТекущийМетод);
            МассивМетодовРегиона.Добавить(ИмяМетода);
            
        КонецЕсли;
        
    КонецЦикла;

    ТекущийИндекс.Вставить("regions", МассивРегионов);
    ТекущийИндекс.Вставить("methods", МассивМетодов);

    Возврат ТекущийИндекс;
    
КонецФункции

Функция РазобратьКомментарийМетода(ТекстовыйДокумент, Метод, Модуль)
    
    ТекущийМетод = Новый Соответствие();
    
    НомерСтроки         = Метод.Начало.НомерСтроки;
    ИмяМетода           = Метод.Сигнатура.Имя;
    
    МассивКомментария = ПарсингКомментария(ТекстовыйДокумент, НомерСтроки);

    Если Не ЗначениеЗаполнено(МассивКомментария) Тогда
        Возврат Неопределено;
    КонецЕсли;

    МассивПараметров  = Новый Массив;
    
    ОписаниеМетода = СформироватьСтруктуруМетода(МассивКомментария, МассивПараметров);
    МассивОписаний = Новый Массив;
    
    Для Каждого СтрокаПараметра Из МассивПараметров Цикл
        ТекущееОписание = СформироватьСтруктуруОписанияПараметра(Метод, СтрокаПараметра);
        МассивОписаний.Добавить(ТекущееОписание);
    КонецЦикла;

    ОписаниеМетода = СокрЛП(ОписаниеМетода);
    
    ТекущийМетод.Вставить("name"       , ИмяМетода);
    ТекущийМетод.Вставить("id"         , вРег(ИмяМетода));
    ТекущийМетод.Вставить("description", ОписаниеМетода);
    ТекущийМетод.Вставить("params"     , МассивОписаний);
    
    Возврат ТекущийМетод;
    
КонецФункции

Функция СформироватьСтруктуруМетода(Знач МассивКомментария, МассивПараметров)
    
    ОписаниеМетода = "";
    
    ЗаписыватьПараметры = Ложь;
    ЗаписыватьОписание  = Истина;
    
    Счетчик = 0;
    Для Каждого СтрокаКомментария Из МассивКомментария Цикл
        
        Счетчик = Счетчик + 1;
        
        Если Не ЗначениеЗаполнено(СокрЛП(СтрокаКомментария)) Тогда
            ЗаписыватьОписание = Ложь;
        КонецЕсли;
        
        Если ЗаписыватьОписание = Истина И Счетчик > 1 Тогда
            ОписаниеМетода = ?(ЗначениеЗаполнено(ОписаниеМетода), ОписаниеМетода + "    |   ", ОписаниеМетода) 
            + СтрокаКомментария;
        КонецЕсли;
        
        Если СтрНайти(СтрокаКомментария, "Параметры:") > 0 Или СтрНайти(СтрокаКомментария, "Parameters:") > 0 Тогда
            ЗаписыватьПараметры = Истина;
            ЗаписыватьОписание  = Ложь;
            
        ИначеЕсли СтрНайти(СтрокаКомментария, "Возвращаемое значение:") > 0 Или СтрНайти(СтрокаКомментария, "Returns:") > 0 Тогда
            Прервать;
            
        ИначеЕсли ЗаписыватьПараметры = Истина 
            И ЗначениеЗаполнено(СокрЛП(СтрокаКомментария)) 
            И Не СтрНачинаетсяС(СокрЛП(СтрокаКомментария), "*") Тогда
            
            МассивПараметров.Добавить(СтрокаКомментария);
            
        Иначе
            Продолжить;
        КонецЕсли;
        
    КонецЦикла;
    
    Возврат ОписаниеМетода;
    
КонецФункции

Функция СформироватьСтруктуруОписанияПараметра(Знач Метод, Знач ПараметрМетода)
    
    ТекущееОписание = Новый Соответствие();

    Разделитель = "-";
    
    МассивЭлементовПараметра = СтрРазделить(ПараметрМетода, Разделитель, Ложь);
    КоличествоЭлементов      = МассивЭлементовПараметра.Количество();
    
    Для Н = 0 По МассивЭлементовПараметра.ВГраница() Цикл
        МассивЭлементовПараметра[Н] = СокрЛП(МассивЭлементовПараметра[Н]);
    КонецЦикла;
    
    Если КоличествоЭлементов < 4 Тогда
        ВызватьИсключение("Недостаточный набор данных в док. комментарии: " + Метод.Сигнатура.Имя);
    КонецЕсли;
     
    Имя1С      = МассивЭлементовПараметра[0];
    Имя        = "--" + МассивЭлементовПараметра[3];
    Буква      = Лев(МассивЭлементовПараметра[3], 1);
    Типы       = МассивЭлементовПараметра[1];
    Описание   = ?(КоличествоЭлементов >= 5, МассивЭлементовПараметра[4], МассивЭлементовПараметра[2]);
    ЗначениеУМ = ПолучитьЗначениеПараметраПоУмолчанию(Имя1С, Метод);
    
    Если КоличествоЭлементов > 5 Или СтрНайти(Имя, " ") > 0 Тогда
        ВызватьИсключение("Некорректный документирующий комментарий в методе: " + Метод.Сигнатура.Имя);
    КонецЕсли;
    
    ТекущееОписание.Вставить("name"       , Имя);
    ТекущееОписание.Вставить("types"      , Типы);
    ТекущееОписание.Вставить("description", Описание);
    ТекущееОписание.Вставить("default"    , ЗначениеУМ);
    
    Возврат ТекущееОписание;

КонецФункции

Функция ПарсингКомментария(Знач ТекстовыйДокумент, Знач НомерСтроки)
    
    ТекущаяСтрока       = ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки - 1);
    ТекстКомментария    = ТекущаяСтрока;
    
    Счетчик	= 1;
    Пока СтрНайти(ТекущаяСтрока, "//") > 0 Цикл
        
        Счетчик = Счетчик + 1;
        
        ТекущаяСтрока    = ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки - Счетчик);
        ТекстКомментария = ТекущаяСтрока + Символы.ПС + ТекстКомментария;
        
    КонецЦикла;
    
    Если СтрНайти(ТекстКомментария, "!NOCLI") > 0 Тогда
        Возврат Новый Массив;
    КонецЕсли;
    
    МассивКомментария = СтрРазделить(ТекстКомментария, "//", Ложь);
    
    Если МассивКомментария.Количество() = 0 Тогда
        Возврат Новый Массив;
    Иначе
        МассивКомментария.Удалить(0);
    КонецЕсли;
    
    Возврат МассивКомментария;
    
КонецФункции

Функция ПолучитьЗначениеПараметраПоУмолчанию(Знач Имя, Знач Метод)
    
    Значение = "";
    
    Для Каждого ПараметрМетода Из Метод.Сигнатура.Параметры Цикл
        
        Если ПараметрМетода.Имя = Имя Тогда
            
            ЗначениеПараметра = ПараметрМетода.Значение;
            Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
                Попытка
                    Значение = ЗначениеПараметра["Элементы"][0]["Значение"];
                Исключение 
                    Значение = ЗначениеПараметра.Значение;
                КонецПопытки;
                Значение = ?(ЗначениеЗаполнено(Значение), Значение, "Пустое значение");
            КонецЕсли;
            
        КонецЕсли;
        
    КонецЦикла;
    
    Возврат Значение;
    
КонецФункции

Функция ПолучитьКаталогиРасширений()

    КаталогиРасширений = Новый Массив;
    КаталогиРасширений.Добавить(КаталогРасширений);
    
    КаталогРасширенийДоп = ХранилищеНастроек.ВернутьНастройку("ext_path");

    Если ЗначениеЗаполнено(КаталогРасширенийДоп) Тогда
        КаталогиРасширений.Добавить(КаталогРасширенийДоп);
    КонецЕсли;

    Возврат КаталогиРасширений;

КонецФункции

Функция Синонимайзер(ИмяРеквизита)
    
    Перем Синоним, ъ, Символ, ПредСимвол, СледСимвол, Прописная, ПредПрописная, СледПрописная, ДлинаСтроки;
    
    Синоним = ВРег(Сред(ИмяРеквизита, 1, 1));
    ДлинаСтроки = СтрДлина(ИмяРеквизита);
    Для ъ=2 По ДлинаСтроки Цикл
        Символ = Сред(ИмяРеквизита, ъ, 1);
        ПредСимвол = Сред(ИмяРеквизита, ъ-1, 1);
        СледСимвол = Сред(ИмяРеквизита, ъ+1, 1);
        Прописная = Символ = ВРег(Символ);
        ПредПрописная = ПредСимвол = ВРег(ПредСимвол);
        СледПрописная = СледСимвол = ВРег(СледСимвол);
        
        // Варианты:
        Если НЕ ПредПрописная И Прописная Тогда
            Синоним = Синоним + " " + Символ;
        ИначеЕсли Прописная И НЕ СледПрописная Тогда
            Синоним = Синоним + " " + Символ;
        Иначе
            Синоним = Синоним + Символ;
        Конецесли;
    КонецЦикла;
    
    Синоним = ВРег(Лев(Синоним,1)) + нРег(Сред(Синоним,2));
    
    Возврат Синоним;
    
КонецФункции

Процедура ДобавитьРасширениеВКэш(Знач Имя, Знач Путь, Знач Количество)

    СтруктураКэша = Новый Структура("filepath,count", Путь, Количество);
    КэшРасширений.Вставить(Имя, СтруктураКэша);

КонецПроцедуры

Процедура ОбработатьИсключениеПодключения(ФайлРасширения, ОписаниеПроблемы, Тест)

    Если Не Тест Тогда
        ДобавитьРасширениеВКэш(ФайлРасширения.ИмяБезРасширения, ФайлРасширения.ПолноеИмя, ОписаниеПроблемы);
    КонецЕсли;

    Сообщить(ОписаниеПроблемы); 

КонецПроцедуры

#КонецОбласти

#Region Alternate

Procedure Initialize(OPIObject_, SettingsVault_, ExtensionsCatalog_, ActionsProcessor_) Export
	Инициализировать(OPIObject_, SettingsVault_, ExtensionsCatalog_, ActionsProcessor_);
EndProcedure

Procedure CompleteCompositionWithExtensions() Export
	ДополнитьСоставРасширениями();
EndProcedure

Function ConnectExtensionFile(ExtensionFile, Test = False) Export
	Return ПодключитьФайлРасширения(ExtensionFile, Test);
EndFunction

Function GetExtensionsList() Export
	Return ПолучитьСписокРасширений();
EndFunction

Function UpdateExtensionsList() Export
	Return ОбновитьСписокРасширений();
EndFunction

Function GetExtensionText(ModuleName) Export
	Return ПолучитьТекстРасширения(ModuleName);
EndFunction

Function SaveExtensionsText(ModuleName, ModuleText) Export
	Return СохранитьТекстРасширения(ModuleName, ModuleText);
EndFunction

Function GetExtensionDirectoryList() Export
	Return ПолучитьСписокКаталоговРасширений();
EndFunction

Function CreateExtensionFile(ModuleName, CreationDirectory) Export
	Return СоздатьФайлРасширения(ModuleName, CreationDirectory);
EndFunction

Function DeleteExtensionFile(ModuleName) Export
	Return УдалитьФайлРасширения(ModuleName);
EndFunction

#EndRegion