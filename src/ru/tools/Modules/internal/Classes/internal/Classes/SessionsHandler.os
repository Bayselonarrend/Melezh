#Использовать "./internal"

Перем СписокСеансов;
Перем МенеджерСоединений;
Перем ХранилищеНастроек;

Перем ОтслеживатьНеудачныеПопытки;
Перем ЧислоПопыток;
Перем ДлительностьБлокировки;

Перем СписокБлокировок;
Перем СписокПопыток;

#Область СлужебныйПрограммныйИнтерфейс

Процедура Инициализировать(МенеджерСоединений_, ХранилищеНастроек_) Экспорт
	
	СписокСеансов      = Новый Соответствие;
	МенеджерСоединений = МенеджерСоединений_;
	ХранилищеНастроек  = ХранилищеНастроек_;
	
	СписокБлокировок = Новый Соответствие();
	СписокПопыток    = Новый Соответствие();
	
КонецПроцедуры

Функция АвторизоватьСеанс(Контекст) Экспорт
	
	Результат = Ложь;
	
	Если Контекст.Запрос.Метод <> "POST" Тогда
		Возврат Toolbox.ОшибкаОбработки(Контекст, 405, "Method Not Allowed");
	КонецЕсли;
	
	IPКлиента = Контекст.Соединение.УдаленныйIpАдрес;

	Если ПроверитьБлокировкуАвторизации(IPКлиента) Тогда
		Возврат Toolbox.ОшибкаОбработки(Контекст, 429, "Превышено число неудачных попыток авторизации. Повторите попытку позже");
	КонецЕсли;
	
	ДлинаКонтента = Контекст.Запрос.ДлинаКонтента;
	ДлинаКонтента = ?(ДлинаКонтента = Неопределено, 0, ДлинаКонтента);
	
	Если ДлинаКонтента > 50000 Тогда
		Возврат Toolbox.ОшибкаОбработки(Контекст, 413, "Payload Too Large");
	КонецЕсли;
	
	ТелоЗапроса  = Контекст.Запрос.Тело;
	
	Попытка
		Пароль = Контекст.Запрос.Форма["password"][0];
	Исключение
		Возврат Toolbox.ОшибкаОбработки(Контекст, 400, "Bad Request");
	КонецПопытки;
	
	Если Пароль = ПарольПроекта() Тогда
		
		Печенье = Строка(Новый УникальныйИдентификатор());
		
		Пока СписокСеансов.Получить(Печенье) <> Неопределено Цикл
			Печенье = Строка(Новый УникальныйИдентификатор());
		КонецЦикла;
		
		СписокСеансов.Вставить(Печенье, Истина);
		
		Контекст.Ответ.Куки.Добавить("melezh", Печенье);
		Контекст.Ответ.КодСостояния = 200;
		
		Результат = Новый Структура("result", Истина);

		СписокПопыток.Удалить(IPКлиента);
		
	Иначе
		
		Результат = Toolbox.ОшибкаОбработки(Контекст, 400, "Неверный пароль!");
		
	КонецЕсли;
	
	ВыполнитьСборкуМусора();
	
	Возврат Результат;
	
КонецФункции

Функция АвторизованныйСеанс(Контекст) Экспорт
	
	ТокенАвторизации = ПолучитьАвторизациюCookie(Контекст);
	
	Если ЗначениеЗаполнено(ТокенАвторизации) Тогда
		
		Авторизован = СписокСеансов.Получить(ТокенАвторизации);
		Авторизован = ?(ЗначениеЗаполнено(Авторизован), Авторизован, Ложь);
		
	Иначе
		Авторизован = Ложь;
	КонецЕсли;
	
	Возврат Авторизован;
	
КонецФункции

Процедура УдалитьСеанс(Контекст) Экспорт
	
	ТокенАвторизации = ПолучитьАвторизациюCookie(Контекст);
	СписокСеансов.Удалить(ТокенАвторизации);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьАвторизациюCookie(Контекст)
	
	Печеньки = Контекст.Запрос.Куки;
	Токен    = "";
	
	Для Каждого Печенька Из Печеньки Цикл
		
		Если Печенька.Ключ = "melezh" Тогда
			Токен = Печенька.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Токен;
	
КонецФункции

Функция ПроверитьБлокировкуАвторизации(IPКлиента)
	
	МаксимальноеЧислоПопыток = ХранилищеНастроек.ВернутьНастройку("auth_attempts");
	ДлительностьБлокировки   = ХранилищеНастроек.ВернутьНастройку("auth_ban_duration");
	
	Если МаксимальноеЧислоПопыток = 0 Или ДлительностьБлокировки = 0 Тогда
		Возврат Ложь;
	КонецЕсли;

	ДействующаяБлокировка = СписокБлокировок.Получить(IPКлиента);

	Если ДействующаяБлокировка <> Неопределено Тогда

		ОставшеесяВремя = ДействующаяБлокировка - ТекущаяДата();

		Если ОставшеесяВремя <= 0 Тогда
			СписокБлокировок.Удалить(IPКлиента);
		Иначе
			Возврат Истина;
		КонецЕсли;

	КонецЕсли;

	ЧислоПопыток = СписокПопыток.Получить(IPКлиента);
	ЧислоПопыток = ?(ЧислоПопыток = Неопределено, 0, ЧислоПопыток);

	Если ЧислоПопыток >= МаксимальноеЧислоПопыток Тогда
		СписокПопыток.Удалить(IPКлиента);
		СписокБлокировок.Вставить(IPКлиента, ТекущаяДата() + ДлительностьБлокировки * 60);
		Возврат Истина;
	Иначе
		ЧислоПопыток = ЧислоПопыток + 1;
		СписокПопыток.Вставить(IPКлиента, ЧислоПопыток);
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ПарольПроекта()
	
	СоединениеRO = МенеджерСоединений.ПолучитьСоединениеRO();
	Пароль       = ХранилищеНастроек.ВернутьНастройку("ui_password");
	
	Если Пароль = Неопределено Тогда
		ВызватьИсключение "Пароль UI не был найден в настройках проекта. Возможно, файл проекта поврежден!";
	Иначе
		Возврат Пароль;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Region Alternate

Procedure Initialize(ConnectionManager_, SettingsVault_) Export
	Инициализировать(ConnectionManager_, SettingsVault_);
EndProcedure

Function AuthorizeSession(Context) Export
	Return АвторизоватьСеанс(Context);
EndFunction

Function AuthorizedSession(Context) Export
	Return АвторизованныйСеанс(Context);
EndFunction

Procedure DeleteSession(Context) Export
	УдалитьСеанс(Context);
EndProcedure

#EndRegion