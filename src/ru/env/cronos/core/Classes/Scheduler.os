Перем ОбъектКомпоненты;

#Область ПрограммныйИнтерфейс

Процедура Инициализировать(Знач СтруктураРасписания = "") Экспорт

	Если ЗначениеЗаполнено(СтруктураРасписания) Тогда

		Если Не ТипЗнч(СтруктураРасписания) = Тип("Структура")
			И Не ТипЗнч(СтруктураРасписания) = Тип("Соответствие") Тогда

			ВызватьИсключение "Расписание должно быть валидной коллекцией ключ и значение!";
		КонецЕсли;

		РасписаниеСтрокой = РасписаниеВСтроку(СтруктураРасписания);

	Иначе
		РасписаниеСтрокой = "";
	КонецЕсли;

	ТекущийПуть = СтрЗаменить(ТекущийСценарий().Каталог, "\", "/");
	ТекущийПуть = СтрРазделить(ТекущийПуть, "/");

	ТекущийПуть.Удалить(ТекущийПуть.ВГраница());
	ТекущийПуть.Удалить(ТекущийПуть.ВГраница());

	ТекущийПуть.Добавить("addins");
	ТекущийПуть.Добавить("Cronos.zip");

	ПодключитьВнешнююКомпоненту(СтрСоединить(ТекущийПуть, "/"), "Cronos", ТипВнешнейКомпоненты.Native);

	ОбъектКомпоненты = Новый("AddIn.Cronos.Main");
	Результат = ОбъектКомпоненты.Init(РасписаниеСтрокой);
	
	РезультатИнициализации = ПрочитатьJSONТекст(Результат);

	Если Не РезультатИнициализации["result"] Тогда
		ВызватьИсключение Результат;
	КонецЕсли
	
КонецПроцедуры

Функция ОжидатьСобытие() Экспорт

	Событие = ОбъектКомпоненты.NextEvent();

	Пока Событие = "" Цикл

		Приостановить(100);
		Событие = ОбъектКомпоненты.NextEvent();

	КонецЦикла;

	Возврат Событие;
	
КонецФункции

Функция ДобавитьЗадание(Знач Имя, Знач Расписание) Экспорт
	Возврат ПрочитатьJSONТекст(ОбъектКомпоненты.AddJob(Имя, Расписание));
КонецФункции

Функция УдалитьЗадание(Знач Имя) Экспорт
	Возврат ПрочитатьJSONТекст(ОбъектКомпоненты.RemoveJob(Имя));
КонецФункции

Функция ИзменитьРасписаниеЗадания(Знач Имя, Знач Расписание) Экспорт
	Возврат ПрочитатьJSONТекст(ОбъектКомпоненты.UpdateJob(Имя, Расписание));
КонецФункции

Функция ВключитьЗадание(Знач Имя) Экспорт
	Возврат ПрочитатьJSONТекст(ОбъектКомпоненты.EnableJob(Имя));
КонецФункции

Функция ОтключитьЗадание(Знач Имя) Экспорт
	Возврат ПрочитатьJSONТекст(ОбъектКомпоненты.DisableJob(Имя));
КонецФункции

Функция ПолучитьСписокЗаданий() Экспорт
	Возврат ПрочитатьJSONТекст(ОбъектКомпоненты.GetJobList());
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрочитатьJSONТекст(Знач Текст)

	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(Текст);

	Результат = ПрочитатьJSON(ЧтениеJSON);

	ЧтениеJSON.Закрыть();

	Возврат Результат;

КонецФункции

Функция РасписаниеВСтроку(Знач Расписание)

	Попытка

		ЗаписьJSON = Новый ЗаписьJSON();
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON, Расписание);
		Возврат ЗаписьJSON.Закрыть();

	Исключение
		ВызватьИсключение "Ошибка преобразования расписания в JSON строку!";
	КонецПопытки;

КонецФункции

#КонецОбласти