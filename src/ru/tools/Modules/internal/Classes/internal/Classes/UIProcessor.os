#Использовать oint
#Использовать "./internal"

Перем ПутьСервера;
Перем ОбработчикСеансов;

#Область СлужебныйПрограммныйИнтерфейс

Процедура Инициализировать(ПутьСервера_, ОбработчикСеансов_) Экспорт

	ПутьСервера       = ПутьСервера_;
	ОбработчикСеансов = ОбработчикСеансов_;

КонецПроцедуры

Функция ОсновнаяОбработка(Знач Контекст, Знач Путь) Экспорт

	Результат = Неопределено;

	Если Путь = "ui" Тогда

		ВернутьСтраницуUI(Контекст);

	ИначеЕсли Путь = "ui/login" Тогда

		Результат = АвторизоватьСеанс(Контекст);

	ИначеЕсли Путь = "ui/logout" Тогда

		ОбработчикСеансов.УдалитьСеанс(Контекст);
		Toolbox.Перенаправление(Контекст, "/ui");

	Иначе
		Результат = Toolbox.ОшибкаОбработки(Контекст, 404, "Not found");
	КонецЕсли;

	ВыполнитьСборкуМусора();

    Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВернутьСтраницуUI(Контекст)

	Контекст.Ответ.КодСостояния = 200;

	Если ОбработчикСеансов.АвторизованныйСеанс(Контекст) Тогда 

		ВернутьСтраницуHTML(Контекст, "console.html");

	Иначе

		ВернутьСтраницуHTML(Контекст, "login.html");

	КонецЕсли;

КонецПроцедуры

Процедура ВернутьСтраницуHTML(Контекст, Путь)

	ПутьОтвета   = Контекст.Ответ.Тело;
	ЗаписьОтвета = Новый ЗаписьДанных(ПутьОтвета);

	ПолныйПутьФайла = СтрШаблон("%1/%2", ПутьСервера, Путь);
	ЗаписьОтвета.Записать(Новый ДвоичныеДанные(ПолныйПутьФайла));

	ЗаписьОтвета.Закрыть();

КонецПроцедуры

Функция АвторизоватьСеанс(Контекст)

	Результат = ОбработчикСеансов.АвторизоватьСеанс(Контекст);

	Возврат Результат;

КонецФункции

#КонецОбласти