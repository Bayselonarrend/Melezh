Перем СписокСеансов;
Перем СоединениеRO;

#Область СлужебныйПрограммныйИнтерфейс

Функция Инициализировать(СоединениеRO_) Экспорт

	СписокСеансов = Новый Соответствие;
	СоединениеRO  = СоединениеRO_;

КонецФункции

Функция АвторизоватьСеанс(Контекст) Экспорт

	Результат = Ложь;

	Если Контекст.Запрос.Метод <> "POST" Тогда
		Возврат ОшибкаОбработки(Контекст, 405, "Method Not Allowed");
	КонецЕсли;

	ДлинаКонтента = Контекст.Запрос.ДлинаКонтента;
	ДлинаКонтента = ?(ДлинаКонтента = Неопределено, 0, ДлинаКонтента);

	Если ДлинаКонтента > 50000 Тогда
		Возврат ОшибкаОбработки(Контекст, 413, "Payload Too Large");
	КонецЕсли;

	ТелоЗапроса  = Контекст.Запрос.Тело;

	Попытка
		Пароль = Контекст.Запрос.Форма["password"][0];
	Исключение
		Возврат ОшибкаОбработки(Контекст, 400, "Bad Request");
	КонецПопытки;

	Если Пароль = ПарольПроекта() Тогда

		Печенье = Строка(Новый УникальныйИдентификатор());

		Пока СписокСеансов.Получить(Печенье) <> Неопределено Цикл
			Печенье = Строка(Новый УникальныйИдентификатор());
		КонецЦикла;

		СписокСеансов.Вставить(Печенье, Истина);

		Контекст.Ответ.Куки.Добавить("melezh", Печенье);
		Контекст.Ответ.КодСостояния = 200;

		Результат = Новый Структура("result", Истина);

	Иначе

		Результат = ОшибкаОбработки(Контекст, 400, "Bad Request");

	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция АвторизованныйСеанс(Контекст) Экспорт

	ТокенАвторизации = ПолучитьАвторизациюCookie(Контекст);

	Если ЗначениеЗаполнено(ТокенАвторизации) Тогда

		Авторизован = СписокСеансов.Получить(ТокенАвторизации);
		Авторизован = ?(ЗначениеЗаполнено(Авторизован), Авторизован, Ложь);

	Иначе
		Авторизован = Ложь;
	КонецЕсли;

	Возврат Авторизован;

КонецФункции

Процедура УдалитьСеанс(Контекст) Экспорт

	ТокенАвторизации = ПолучитьАвторизациюCookie(Контекст);
	СписокСеансов.Удалить(ТокенАвторизации);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьАвторизациюCookie(Контекст)

	Печеньки = Контекст.Запрос.Куки;
	Токен    = "";

	Для Каждого Печенька Из Печеньки Цикл

		Если Печенька.Ключ = "melezh" Тогда
			Токен = Печенька.Значение;
		КонецЕсли;

	КонецЦикла;

	Возврат Токен;

КонецФункции

Функция ПарольПроекта()
	Возврат "12345";
КонецФункции

Функция ОшибкаОбработки(Контекст, Код, Текст)

    Контекст.Ответ.КодСостояния = Код;

    Возврат Новый Структура("result,error", Ложь, Текст);

КонецФункции

#КонецОбласти