#Использовать "./internal"

Перем ОбъектОПИ;
Перем ХранилищеНастроек;
Перем КаталогРасширений;
Перем КэшРасширений;
Перем ПроцессорДействий;

#Область СлужебныйПрограммныйИнтерфейс

Процедура Инициализировать(ОбъектОПИ_, ХранилищеНастроек_, КаталогРасширений_, ПроцессорДействий_) Экспорт
	
	ОбъектОПИ          = ОбъектОПИ_;
	ХранилищеНастроек  = ХранилищеНастроек_;
    КаталогРасширений  = КаталогРасширений_;
    КэшРасширений      = Новый Соответствие;
    ПроцессорДействий  = ПроцессорДействий_;

    ДополнитьСоставРасширениями();
	
КонецПроцедуры

Процедура ДополнитьСоставРасширениями() Экспорт

    КаталогиРасширений = ПолучитьКаталогиРасширений();

    Для Каждого ТекущийКаталогРасширений Из КаталогиРасширений Цикл
        
        ФайлыРасширений = НайтиФайлы(ТекущийКаталогРасширений, "*.os");

        Для Каждого ФайлРасширения Из ФайлыРасширений Цикл

            Попытка

                ИмяРасширения = ФайлРасширения.ИмяБезРасширения;

                Если Не Toolbox.СтрокаНачинаетсяСБуквы(ИмяРасширения) Тогда
                    Сообщить(СтрШаблон("Ошибка применения расширения %1: имя модуля должно начинаться с буквы", ИмяРасширения));
                    Продолжить;
                КонецЕсли;

                ТаблицаПараметров = РазобратьМодуль(ФайлРасширения);
                ОбъектОПИ.ДополнитьКэшСостава(ФайлРасширения.ИмяБезРасширения, ТаблицаПараметров); 
                ПроцессорДействий.ПодключитьСценарийРасширения(ФайлРасширения.ПолноеИмя, ФайлРасширения.ИмяБезРасширения);

            Исключение

                ОписаниеПроблемы = СтрШаблон("Ошибка применения расширения: %1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

                ДобавитьРасширениеВКэш(ФайлРасширения.ИмяБезРасширения, ФайлРасширения.ПолноеИмя, ОписаниеПроблемы);
                Сообщить(ОписаниеПроблемы);

            КонецПопытки;

        КонецЦикла;

    КонецЦикла;

КонецПроцедуры

Функция ПолучитьСписокРасширений() Экспорт

    МассивРасширений = Новый Массив;

    Для Каждого Расширение Из КэшРасширений Цикл

        ТекущееРасширение = Новый Структура;
        ТекущееРасширение.Вставить("name"    , Расширение.Ключ);
        ТекущееРасширение.Вставить("filepath", Расширение.Значение["filepath"]);
        ТекущееРасширение.Вставить("count"   , Расширение.Значение["count"]);
        
        МассивРасширений.Добавить(ТекущееРасширение);

    КонецЦикла;

    Возврат Новый Структура("result,data", Истина, МассивРасширений);

КонецФункции

Функция ОбновитьСписокРасширений() Экспорт

    КэшРасширений = Новый Соответствие;
    ОбъектОПИ.ИнициализироватьОсновныеСписки();
    ПроцессорДействий.ОчиститьСписокАктивныхРасширений();
    ДополнитьСоставРасширениями();
    
    Возврат Новый Структура("result", Истина);

КонецФункции

Функция ПолучитьТекстРасширения(ИмяМодуля) Экспорт

    Расширение = КэшРасширений.Получить(ИмяМодуля);

    Если Расширение = Неопределено Тогда
        Результат = Новый Структура("result,error", Ложь, "Расширение с указанным именем не найдено!");
    Иначе

        Текст = ПолучитьСтрокуИзДвоичныхДанных(Новый ДвоичныеДанные(Расширение["filepath"]));
        Результат = Новый Структура("result,text", Истина, Текст);

    КонецЕсли;

    Возврат Результат;

КонецФункции

Функция СохранитьТекстРасширения(ИмяМодуля, ТекстМодуля) Экспорт

    Расширение = КэшРасширений.Получить(ИмяМодуля);

    Если Расширение = Неопределено Тогда
        Результат = Новый Структура("result,error", Ложь, "Расширение с указанным именем не найдено!");
    Иначе

        ТекстДД   = ПолучитьДвоичныеДанныеИзСтроки(ТекстМодуля);
        ТекстДД.Записать(Расширение["filepath"]);

        Результат = ОбновитьСписокРасширений();

    КонецЕсли;

    Возврат Результат;

КонецФункции

Функция ПолучитьСписокКаталоговРасширений() Экспорт
    Возврат Новый Структура("result,data", Истина, ПолучитьКаталогиРасширений());
КонецФункции

Функция СоздатьФайлРасширения(ИмяМодуля, КаталогСоздания) Экспорт

    Если Не Toolbox.СтрокаНачинаетсяСБуквы(ИмяМодуля) Тогда
        Возврат Новый Структура("result,error,code", Ложь, "Имя модуля должно начинаться с буквы!", 400);
    КонецЕсли;

    КаталогиРасширений = ПолучитьКаталогиРасширений();
    ПереданДоступный   = Ложь;
    
    Для Каждого ТекущийКаталогРасширений Из КаталогиРасширений Цикл

        Если ТекущийКаталогРасширений = КаталогСоздания Тогда
            ПереданДоступный = Истина;
        КонецЕсли;

    КонецЦикла;

    Если Не ПереданДоступный Тогда
        Возврат Новый Структура("result,error,code", Ложь, "Передан некорректный каталог расширений", 404);
    КонецЕсли;

    Если КэшРасширений.Получить(ИмяМодуля) <> Неопределено Тогда
        Возврат Новый Структура("result,error,code", Ложь, "Расширение с таким именем уже существует", 400);
    КонецЕсли;

    ПутьСохранения = СтрЗаменить(КаталогСоздания, "\", "/");
    ПутьСохранения = ?(СтрЗаканчиваетсяНа(ПутьСохранения, "/"), ПутьСохранения, ПутьСохранения + "/");
    ПутьСохранения = СтрШаблон("%1%2.os", ПутьСохранения, ИмяМодуля);

    ПолучитьДвоичныеДанныеИзСтроки("").Записать(ПутьСохранения);

    Результат = ОбновитьСписокРасширений();

    Возврат Результат;
    
КонецФункции

Функция УдалитьФайлРасширения(ИмяМодуля) Экспорт

    Расширение = КэшРасширений.Получить(ИмяМодуля);

    Если Расширение = Неопределено Тогда
        Результат = Новый Структура("result,error", Ложь, "Расширение с указанным именем не найдено!");
    Иначе
      
        УдалитьФайлы(Расширение["filepath"]);
        Результат = ОбновитьСписокРасширений();

    КонецЕсли;

    Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазобратьМодуль(Модуль)
    
    ТаблицаСостава = Новый ТаблицаЗначений();
    ТаблицаСостава.Колонки.Добавить("Библиотека");
    ТаблицаСостава.Колонки.Добавить("Модуль");
    ТаблицаСостава.Колонки.Добавить("Метод");
    ТаблицаСостава.Колонки.Добавить("МетодПоиска");
    ТаблицаСостава.Колонки.Добавить("Параметр");
    ТаблицаСостава.Колонки.Добавить("ПараметрСокр");
    ТаблицаСостава.Колонки.Добавить("Описание");
    ТаблицаСостава.Колонки.Добавить("ОписаниеМетода");
    ТаблицаСостава.Колонки.Добавить("Область");
    
    Парсер         = Новый("EmbeddedLanguageParser");
    ДокументМодуля = Новый ТекстовыйДокумент;
    ДокументМодуля.Прочитать(Модуль.ПолноеИмя, "UTF-8");

    ТекстМодуля     = ДокументМодуля.ПолучитьТекст();
    СтруктураМодуля = Парсер.Parse(ТекстМодуля);
    ТекущаяОбласть  = "Основные методы";

    СчетчикМетодов = 0;
    Для Каждого Метод Из СтруктураМодуля.Declarations Цикл
  
        Если Метод.Type = "InstructionPreprocessorRegion" Тогда
            ТекущаяОбласть = Синонимайзер(Метод.Name);
        КонецЕсли;
        
        Если Метод.Type = "MethodDeclaration" И Метод.Signature.Export = Истина Тогда

            РазобратьКомментарийМетода(ДокументМодуля, Метод, Модуль, ТекущаяОбласть, ТаблицаСостава);	 

            Если ЗначениеЗаполнено(ТаблицаСостава) Тогда
                СчетчикМетодов = СчетчикМетодов + 1;      
            КонецЕсли;

        КонецЕсли;
        
    КонецЦикла;

    ДобавитьРасширениеВКэш(Модуль.ИмяБезРасширения, Модуль.ПолноеИмя, СчетчикМетодов);

    Возврат ТаблицаСостава;

КонецФункции

Процедура РазобратьКомментарийМетода(ТекстовыйДокумент, Метод, Модуль, Область, ТаблицаСостава)
    
    НомерСтроки         = Метод.Start.LineNumber;
    ИмяМетода           = Метод.Signature.Name;
    
    МассивКомментария = ПарсингКомментария(ТекстовыйДокумент, НомерСтроки);
    
    Если МассивКомментария.Количество() = 0 Тогда
        Возврат;
    КонецЕсли;
    
    МассивПараметров  = Новый Массив;
    ОписаниеМетода    = "";
    
    СформироватьСтруктуруМетода(МассивКомментария, МассивПараметров, ОписаниеМетода);
    СформироватьТаблицуОписанийПараметров(МассивПараметров, Метод, ТаблицаСостава, Модуль, ОписаниеМетода, Область);
    
КонецПроцедуры

Функция ПарсингКомментария(Знач ТекстовыйДокумент, Знач НомерСтроки)
    
    ТекущаяСтрока       = ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки - 1);
    ТекстКомментария    = ТекущаяСтрока;
    
    Счетчик	= 1;
    Пока СтрНайти(ТекущаяСтрока, "//") > 0 Цикл
        
        Счетчик = Счетчик + 1;
        
        ТекущаяСтрока    = ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки - Счетчик);
        ТекстКомментария = ТекущаяСтрока + Символы.ПС + ТекстКомментария;
        
    КонецЦикла;
    
    Если СтрНайти(ТекстКомментария, "!NOCLI") > 0 Тогда
        Возврат Новый Массив;
    КонецЕсли;
    
    МассивКомментария = СтрРазделить(ТекстКомментария, "//", Ложь);
    
    Если МассивКомментария.Количество() = 0 Тогда
        Сообщить(ТекстКомментария);
        Возврат Новый Массив;
    Иначе
        МассивКомментария.Удалить(0);
    КонецЕсли;
    
    Возврат МассивКомментария;
    
КонецФункции

Процедура СформироватьСтруктуруМетода(Знач МассивКомментария, МассивПараметров, ОписаниеМетода)
    
    ЗаписыватьПараметры = Ложь;
    ЗаписыватьОписание  = Истина;
    
    Счетчик = 0;
    Для Каждого СтрокаКомментария Из МассивКомментария Цикл
        
        Счетчик = Счетчик + 1;
        
        Если Не ЗначениеЗаполнено(СокрЛП(СтрокаКомментария)) Тогда
            ЗаписыватьОписание = Ложь;
        КонецЕсли;
        
        Если ЗаписыватьОписание = Истина И Счетчик > 1 Тогда
            ОписаниеМетода = ОписаниеМетода + СтрокаКомментария;
        КонецЕсли;
        
        Если СтрНайти(СтрокаКомментария, "Параметры:") > 0 Или СтрНайти(СтрокаКомментария, "Parameters:") > 0 Тогда
            ЗаписыватьПараметры = Истина;
            ЗаписыватьОписание  = Ложь;
            
        ИначеЕсли СтрНайти(СтрокаКомментария, "Возвращаемое значение:") > 0 Или СтрНайти(СтрокаКомментария, "Returns:") > 0 Тогда
            Прервать;
            
        ИначеЕсли ЗаписыватьПараметры = Истина 
            И ЗначениеЗаполнено(СокрЛП(СтрокаКомментария)) 
            И Не СтрНачинаетсяС(СокрЛП(СтрокаКомментария), "*") Тогда
            
            МассивПараметров.Добавить(СтрокаКомментария);
            
        Иначе
            Продолжить;
        КонецЕсли;
        
    КонецЦикла;
    
КонецПроцедуры

Процедура СформироватьТаблицуОписанийПараметров(Знач МассивПараметров, Знач Метод, ТаблицаСостава, Модуль, ОписаниеМетода, Область)
    
    Разделитель   = "-";
    МассивТекущих = Новый Массив;
    
    Для Каждого ПараметрМетода Из МассивПараметров Цикл
        
        МассивЭлементовПараметра = СтрРазделить(ПараметрМетода, Разделитель, Ложь);
        КоличествоЭлементов      = МассивЭлементовПараметра.Количество();
        
        Для Н = 0 По МассивЭлементовПараметра.ВГраница() Цикл
            МассивЭлементовПараметра[Н] = СокрЛП(МассивЭлементовПараметра[Н]);
        КонецЦикла;
        
        Если КоличествоЭлементов < 4 Тогда
            
            Для Каждого Текущий Из МассивТекущих Цикл
                ТаблицаСостава.Удалить(Текущий);
            КонецЦикла;

            Возврат;
        КонецЕсли;
         
        Имя1С     = МассивЭлементовПараметра[0];
        Имя       = "--" + МассивЭлементовПараметра[3];
        Типы      = МассивЭлементовПараметра[1];
        Описание  = ?(КоличествоЭлементов >= 5, МассивЭлементовПараметра[4], МассивЭлементовПараметра[2]);
        
        Если КоличествоЭлементов > 5 Или СтрНайти(Имя, " ") > 0 Тогда
            Для Каждого Текущий Из МассивТекущих Цикл
                ТаблицаСостава.Удалить(Текущий);
            КонецЦикла;
            Возврат;
        КонецЕсли;

        Значение   = ПолучитьЗначениеПараметраПоУмолчанию(Имя1С, Метод);
        Библиотека = Модуль.ИмяБезрасширения;
        
        Если ЗначениеЗаполнено(Значение) Тогда
            Описание = Описание + " (необяз. по ум. - " + Значение + ")";
        КонецЕсли;
        
        НоваяСтрока = ТаблицаСостава.Добавить();
        НоваяСтрока.Библиотека  = Библиотека;
        НоваяСтрока.Модуль      = Библиотека;
        НоваяСтрока.Метод       = Метод.Signature.Name;
        НоваяСтрока.МетодПоиска = вРег(НоваяСтрока.Метод);
        НоваяСтрока.Описание    = Описание;
        НоваяСтрока.Область     = Область;
        НоваяСтрока.Параметр    = Имя;

        Если ЗначениеЗаполнено(ОписаниеМетода) Тогда
            НоваяСтрока.ОписаниеМетода = ОписаниеМетода;
        КонецЕсли;       

        МассивТекущих.Добавить(НоваяСтрока);

    КонецЦикла;
    
КонецПроцедуры

Функция ПолучитьЗначениеПараметраПоУмолчанию(Знач Имя, Знач Метод)
    
    Значение = "";
    
    Для Каждого ПараметрМетода Из Метод.Signature.Parameters Цикл
        
        Если ПараметрМетода.Name = Имя Тогда
            
            ЗначениеПараметра = ПараметрМетода.Value;
            Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
                Попытка
                    Значение = ЗначениеПараметра["Items"][0]["Value"];
                Исключение 
                    Значение = ЗначениеПараметра.Value;
                КонецПопытки;
                Значение = ?(ЗначениеЗаполнено(Значение), Значение, "Пустое значение");
            КонецЕсли;
            
        КонецЕсли;
        
    КонецЦикла;
    
    Возврат Значение;
    
КонецФункции

Функция ПолучитьКаталогиРасширений()

    КаталогиРасширений = Новый Массив;
    КаталогиРасширений.Добавить(КаталогРасширений);
    
    КаталогРасширенийДоп = ХранилищеНастроек.ВернутьНастройку("ext_path");

    Если ЗначениеЗаполнено(КаталогРасширенийДоп) Тогда
        КаталогиРасширений.Добавить(КаталогРасширенийДоп);
    КонецЕсли;

    Возврат КаталогиРасширений;

КонецФункции

Функция Синонимайзер(ИмяРеквизита)
    
    Перем Синоним, ъ, Символ, ПредСимвол, СледСимвол, Прописная, ПредПрописная, СледПрописная, ДлинаСтроки;
    
    Синоним = ВРег(Сред(ИмяРеквизита, 1, 1));
    ДлинаСтроки = СтрДлина(ИмяРеквизита);
    Для ъ=2 По ДлинаСтроки Цикл
        Символ = Сред(ИмяРеквизита, ъ, 1);
        ПредСимвол = Сред(ИмяРеквизита, ъ-1, 1);
        СледСимвол = Сред(ИмяРеквизита, ъ+1, 1);
        Прописная = Символ = ВРег(Символ);
        ПредПрописная = ПредСимвол = ВРег(ПредСимвол);
        СледПрописная = СледСимвол = ВРег(СледСимвол);
        
        // Варианты:
        Если НЕ ПредПрописная И Прописная Тогда
            Синоним = Синоним + " " + Символ;
        ИначеЕсли Прописная И НЕ СледПрописная Тогда
            Синоним = Синоним + " " + Символ;
        Иначе
            Синоним = Синоним + Символ;
        Конецесли;
    КонецЦикла;
    
    Синоним = ВРег(Лев(Синоним,1)) + нРег(Сред(Синоним,2));
    
    Возврат Синоним;
    
КонецФункции

Процедура ДобавитьРасширениеВКэш(Знач Имя, Знач Путь, Знач Количество)

    СтруктураКэша = Новый Структура("filepath,count", Путь, Количество);
    КэшРасширений.Вставить(Имя, СтруктураКэша);

КонецПроцедуры

#КонецОбласти