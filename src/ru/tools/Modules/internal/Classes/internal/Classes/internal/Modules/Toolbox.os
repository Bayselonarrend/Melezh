#Использовать oint

#Область СлужебныйПрограммныйИнтерфейс

Функция СоздатьСоединениеRO(Знач Путь) Экспорт

    ПутьПроектаRO   = СтрШаблон("file:%1?mode=ro", Путь);
    СоединениеRO    = OPI_SQLite.ОткрытьСоединение(ПутьПроектаRO);

    Если Не OPI_Компоненты.ЭтоКомпонента(СоединениеRO)  Тогда
        ВызватьИсключение OPI_Инструменты.JSONСтрокой(СоединениеRO);
    Иначе
        Возврат СоединениеRO;
    КонецЕсли;

КонецФункции

Функция СоздатьСоединениеRW(Знач Путь) Экспорт

    СоединениеRW    = OPI_SQLite.ОткрытьСоединение(Путь);

    Если Не OPI_Компоненты.ЭтоКомпонента(СоединениеRW)  Тогда
        ВызватьИсключение OPI_Инструменты.JSONСтрокой(СоединениеRW);
    Иначе
        Возврат СоединениеRW;
    КонецЕсли;

КонецФункции

Функция ПолучитьJSON(Контекст) Экспорт

    ЧтениеДанных    = Новый ЧтениеДанных(Контекст.Запрос.Тело);
    РезультатЧтения = ЧтениеДанных.Прочитать();
    Данные          = РезультатЧтения.ПолучитьДвоичныеДанные(); 

    JSON = Новый ЧтениеJSON();
    JSON.УстановитьСтроку(ПолучитьСтрокуИзДвоичныхДанных(Данные));
    Результат = ПрочитатьJSON(JSON, Истина);

    Возврат Результат;

КонецФункции

Функция Перенаправление(Контекст, Путь) Экспорт
	Контекст.Ответ.КодСостояния = 303;
	Контекст.Ответ.Заголовки["Location"] = Путь;
КонецФункции

Процедура ВернутьСтраницуHTML(Контекст, ПутьСервера, Путь) Экспорт

	ПутьОтвета   = Контекст.Ответ.Тело;
	ЗаписьОтвета = Новый ЗаписьДанных(ПутьОтвета);

	ПолныйПутьФайла = СтрШаблон("%1/%2", ПутьСервера, Путь);
	ЗаписьОтвета.Записать(Новый ДвоичныеДанные(ПолныйПутьФайла));

	ЗаписьОтвета.Закрыть();

КонецПроцедуры

Функция ОшибкаОбработки(Контекст, Код, Текст) Экспорт

    Если ТипЗнч(Текст) = Тип("ИнформацияОбОшибке") Тогда

        Результат  = Новый Структура("result,error", Ложь, Текст.Описание);

        Если СтрНайти(Текст.ИсходнаяСтрока, "ВызватьИсключение") = 0 Тогда

            ФайлМодуля = Новый Файл(Текст.ИмяМодуля);

            СтруктураИсключения = Новый Структура;
            СтруктураИсключения.Вставить("module", ФайлМодуля.Имя);
            СтруктураИсключения.Вставить("row"   , Текст.НомерСтроки);
            СтруктураИсключения.Вставить("code"  , СокрЛП(Текст.ИсходнаяСтрока));

            Результат.Вставить("exception", СтруктураИсключения);

        КонецЕсли;

    Иначе
        Результат = Новый Структура("result,error", Ложь, Текст);
    КонецЕсли;

    Контекст.Ответ.КодСостояния = Код;
    Возврат Результат;

КонецФункции

Функция ПолучитьБулево(Знач Значение) Экспорт

	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		Значение = вРег(Значение);
	КонецЕсли;

	СоответствиеБулево = Новый Соответствие();
	СоответствиеБулево.Вставить(Истина  , Истина);
	СоответствиеБулево.Вставить(1       , Истина);
	СоответствиеБулево.Вставить("1"     , Истина);
	СоответствиеБулево.Вставить("ИСТИНА", Истина);
	СоответствиеБулево.Вставить("TRUE"  , Истина);

	ЗначениеБулево = СоответствиеБулево.Получить(Значение);
	Возврат ?(ЗначениеБулево = Неопределено, Ложь, Истина);
	
КонецФункции

#КонецОбласти