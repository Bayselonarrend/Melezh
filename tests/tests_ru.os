#Использовать asserts
#Использовать "./internal"

Перем ПутьПроекта;

#Область Тесты

&ПередКаждым
Процедура Инициализировать() Экспорт

	ПутьПроекта = ПолучитьИмяВременногоФайла();

	Опции = Новый Структура;
	Опции.Вставить("path", ПутьПроекта);

	Результат = TestTools.ВыполнитьТестCLI("СоздатьПроект", Опции);

КонецПроцедуры

&ПослеКаждого
Процедура Завершение() Экспорт
	УдалитьФайлы(ПутьПроекта);
КонецПроцедуры

&Тест
Процедура СоздатьПроект() Экспорт

	Проект = ПолучитьИмяВременногоФайла();

	Опции = Новый Структура;
	Опции.Вставить("path", Проект);

	Результат = TestTools.ВыполнитьТестCLI("СоздатьПроект", Опции);

	Ожидаем.Что(Результат["path"]).Равно(Проект);
	Ожидаем.Что(Результат["result"]).Равно(Истина);

	TestTools.ЗаписатьЛог(Результат, "СоздатьПроект");

	УдалитьФайлы(Проект);

КонецПроцедуры

&Тест
Процедура ПолучитьНастройкиПроекта() Экспорт

	Опции = Новый Структура;
	Опции.Вставить("proj", ПутьПроекта);

	Результат = TestTools.ВыполнитьТестCLI("ПолучитьНастройкиПроекта", Опции);

	Ожидаем.Что(Результат["result"]).Равно(Истина);
	Ожидаем.Что(Результат["data"]).ИмеетТип("Массив");

	TestTools.ЗаписатьЛог(Результат, "ПолучитьНастройкиПроекта");

КонецПроцедуры

&Тест
Процедура ЗаполнитьНастройкиПроекта() Экспорт

	Настройки = Новый Соответствие();
	Настройки.Вставить("logs_req_headers", Ложь);
	Настройки.Вставить("logs_req_body"   , Ложь);

	Опции = Новый Структура;
	Опции.Вставить("proj", ПутьПроекта);
	Опции.Вставить("set" , Настройки);

	Результат = TestTools.ВыполнитьТестCLI("ЗаполнитьНастройкиПроекта", Опции);

	Ожидаем.Что(Результат["result"]).Равно(Истина);
	
	TestTools.ЗаписатьЛог(Результат, "ЗаполнитьНастройкиПроекта");

	Опции.Удалить("set");

	Результат = TestTools.ВыполнитьТестCLI("ПолучитьНастройкиПроекта", Опции);
	Headers   = Ложь;
	Body      = Ложь;

	Для Каждого ТекущаяНастройка Из Результат["data"] Цикл
		
		Если ТекущаяНастройка["name"] = "logs_req_headers" Тогда
			Ожидаем.Что(ТекущаяНастройка["value"]).Равно("false");
			Headers = Истина;
		КонецЕсли;

		Если ТекущаяНастройка["name"] = "logs_req_body" Тогда
			Ожидаем.Что(ТекущаяНастройка["value"]).Равно("false");
			Body = Истина;
		КонецЕсли;

	КонецЦикла;

	Ожидаем.Что(Headers И Body).Равно(Истина);

КонецПроцедуры

&Тест
Процедура УстановитьНастройкуПроекта() Экспорт

	Опции = Новый Структура;
	Опции.Вставить("proj" , ПутьПроекта);
	Опции.Вставить("key"  , "logs_path");
	Опции.Вставить("value", "yo");

	Результат = TestTools.ВыполнитьТестCLI("УстановитьНастройкуПроекта", Опции);

	Ожидаем.Что(Результат["result"]).Равно(Истина);
	
	TestTools.ЗаписатьЛог(Результат, "УстановитьНастройкуПроекта");

	Опции.Удалить("key");
	Опции.Удалить("value");

	Результат = TestTools.ВыполнитьТестCLI("ПолучитьНастройкиПроекта", Опции);
	Path      = Ложь;

	Для Каждого ТекущаяНастройка Из Результат["data"] Цикл
		
		Если ТекущаяНастройка["name"] = "logs_path" Тогда
			Ожидаем.Что(ТекущаяНастройка["value"]).Равно("yo");
			Path = Истина;
		КонецЕсли;

	КонецЦикла;

	Ожидаем.Что(Path).Равно(Истина);

КонецПроцедуры

#КонецОбласти