#Использовать "./internal"

Перем НастройкиПроектаПолные;
Перем НастройкиПроектаUI;
Перем НастройкиПроекта;
Перем ПоследнееОбновление;

Перем МенеджерСоединений;
Перем МодульПрокси;

#Область СлужебныйПрограммныйИнтерфейс

Процедура Инициализировать(МенеджерСоединений_, МодульПрокси_) Экспорт
	
	МодульПрокси        = МодульПрокси_;
	МенеджерСоединений  = МенеджерСоединений_;
	ПоследнееОбновление = ТекущаяДата();
	
	ЗаполнитьНастройки(Истина);
	
КонецПроцедуры

Функция ВернутьНастройкиПроектаПолные() Экспорт
	
	ЗаполнитьНастройки();
	Возврат НастройкиПроектаПолные;
	
КонецФункции

Функция ВернутьНастройкиПроектаUI() Экспорт
	
	ЗаполнитьНастройки();
	Возврат НастройкиПроектаUI;
	
КонецФункции

Функция ВернутьНастройку(Знач Имя) Экспорт
	
	ЗаполнитьНастройки();
	Настройка = НастройкиПроекта.Получить(Имя);

	Возврат Настройка;
	
КонецФункции

Функция ВернутьБазовыйПуть() Экспорт

	БазовыйПуть = Строка(ВернутьНастройку("base_path"));

    БазовыйПуть = ?(СтрНачинаетсяС(БазовыйПуть    , "/"), БазовыйПуть, "/" + БазовыйПуть);
    БазовыйПуть = ?(СтрЗаканчиваетсяНа(БазовыйПуть, "/"), БазовыйПуть, БазовыйПуть + "/");

	Возврат БазовыйПуть;

КонецФункции

Функция ЗаписатьНастройкиПроекта(Знач Данные) Экспорт
	
	СоединениеRW = МенеджерСоединений.ПолучитьСоединениеRW();
	Результат    = МодульПрокси.ЗаполнитьНастройкиПроекта(СоединениеRW, Данные);
	ЗаполнитьНастройки(Истина);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНастройки(Знач Принудительно = Ложь)

	ТекущаяДата = ТекущаяДата();
	
	Если Принудительно Или ТекущаяДата > ПоследнееОбновление + 60 Тогда
		ПоследнееОбновление = ТекущаяДата;
	Иначе
		Возврат;
	КонецЕсли;
	
	СоединениеRO    = МенеджерСоединений.ПолучитьСоединениеRO();
	НастройкиИзБазы = МодульПрокси.ПолучитьНастройкиПроекта(СоединениеRO);
	
	Если Не НастройкиИзБазы["result"] Тогда
		ВызватьИсключение НастройкиИзБазы["error"];	
	КонецЕсли;
	
	НастройкиПроектаПолные = НастройкиИзБазы;
	НастройкиПроекта       = Новый Соответствие();
	ТаблицаНастроекUI      = Новый Массив;
	
	Для Каждого Настройка Из НастройкиИзБазы["data"] Цикл

		НастройкиПроекта.Вставить(Настройка["name"], Настройка["value"]);

		Если Не Настройка["name"] = "ui_password" Тогда
			ТаблицаНастроекUI.Добавить(Настройка);
		КонецЕсли;

	КонецЦикла;

	НастройкиПроектаUI = Новый Структура("result,data", истина, ТаблицаНастроекUI);
	
КонецПроцедуры

#КонецОбласти


#Region Alternate

Procedure Initialize(ConnectionManager_, ProxyModule_) Export
	Инициализировать(ConnectionManager_, ProxyModule_);
EndProcedure

Function ReturnProjectSettingsFull() Export
	Return ВернутьНастройкиПроектаПолные();
EndFunction

Function ReturnProjectSettingsUI() Export
	Return ВернутьНастройкиПроектаUI();
EndFunction

Function ReturnSetting(Val Name) Export
	Return ВернутьНастройку(Name);
EndFunction

Function ReturnBasePath() Export
	Return ВернутьБазовыйПуть();
EndFunction

Function WriteProjectSettings(Val Data) Export
	Return ЗаписатьНастройкиПроекта(Data);
EndFunction

#EndRegion