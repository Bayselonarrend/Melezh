#Использовать "./internal"

Процедура ОсновнаяОбработка()

	КаталогОригинала = "./src/ru";
	КаталогОригинала = Новый Файл(КаталогОригинала);
	КаталогОригинала = КаталогОригинала.ПолноеИмя;

	ВсеФайлы = НайтиФайлы(КаталогОригинала, "*", Истина);
	Файлы    = Новый Массив;
	
	Для Каждого ФайлПроекта Из ВсеФайлы Цикл
		Файлы.Добавить(ФайлПроекта);
	КонецЦикла;

	МетодыCLI.СформироватьCLI();
	
	ЕстьНовыеСлова = МетодыСловарей.СоздатьНаборСловарей(Файлы);

	Если ЕстьНовыеСлова Тогда
		Сообщить("В словарях есть новые слова без перевода. Перевод и создание CLI не выполнено!");
		ЗавершитьРаботу(111);
	КонецЕсли;

	МетодыПеревода.ПеревестиПроект(Файлы, КаталогОригинала);
	МетодыДокументации.СоздатьДокументацию();
	ОбновитьХэшСумму();

КонецПроцедуры

Процедура ОбновитьХэшСумму()

	Сообщить(СтрШаблон("Начало расчета хеш-суммы: %1", ТекущаяДата()));

	Хеширование = Новый ХешированиеДанных(ХешФункция.SHA256);
    
	Для Каждого Файл Из НайтиФайлы("./src/ru", "*", Истина) Цикл
		Если Не Файл.ЭтоКаталог() И Не Файл.ИмяБезРасширения = "BuildHash" Тогда
			Хеширование.Добавить(Новый ДвоичныеДанные(Файл.ПолноеИмя));
		КонецЕсли;
	КонецЦикла;

	Для Каждого Файл Из НайтиФайлы("./src/en", "*", Истина) Цикл
		Если Не Файл.ЭтоКаталог() И Не Файл.ИмяБезРасширения = "BuildHash" Тогда
			Хеширование.Добавить(Новый ДвоичныеДанные(Файл.ПолноеИмя));
		КонецЕсли;
	КонецЦикла;

    Сумма = Хеширование.ХешСуммаСтрокой;

	Сообщить(СтрШаблон("Окончание расчета хеш-суммы: %1", ТекущаяДата()));
	Сообщить(СтрШаблон("Хеш сумма: %1", Сумма));
	
	Для Каждого Файл Из НайтиФайлы("./src", "*", Истина) Цикл

		Если Не Файл.ИмяБезРасширения = "BuildHash" Тогда
			Продолжить;
		КонецЕсли;

		ПутьМодуляХеша     = Файл.ПолноеИмя;
		ПутьМодуляХеша     = СтрЗаменить(ПутьМодуляХеша, "\", "/");

		МодульИнструментов = Новый ТекстовыйДокумент();
		МодульИнструментов.Прочитать(ПутьМодуляХеша);

		Для Н = 1 По МодульИнструментов.КоличествоСтрок() Цикл

			ТекущаяСтрока = МодульИнструментов.ПолучитьСтроку(Н);

			Если СтрНайти(ТекущаяСтрока, "LastBuildHash") > 0 Тогда
				МодульИнструментов.ЗаменитьСтроку(Н, СтрШаблон("    LastBuildHash  = ""%1"";", Сумма));
				Прервать;
			КонецЕсли;

		КонецЦикла;

		МодульИнструментов.Записать(ПутьМодуляХеша, , Символы.ПС);

	КонецЦикла;

	СуммаДД = ПолучитьДвоичныеДанныеИзСтроки(Сумма);

	СуммаДД.Записать("./service/last_build_hash.txt");

КонецПроцедуры

ОсновнаяОбработка();